model{                                                                       # *** PROGRAM STARTS
for(i in 1:ns){                                                               # LOOP THROUGH STUDIES
  pi2[i] <- coll12[i] * theta1_pred[i] + (1 - coll12[i])*theta2_pred[i]       # Impute pi2 for different collaps types
  w[i,1] <- 0                                                                # adjustment for multi-arm trials is zero for control arm
  delta[i,1] <- 0                                                            # treatment effect is zero for control arm
  mu[i] ~ dnorm(0,.0001)                                              # vague priors for all trial baselines
  for (k in 1:na[i]) {                                                       # LOOP THROUGH GROUPS
    r[i,k] ~ dbin(p[i,k],n[i,k])                                           # binomial likelihood
    logit(p[i,k]) <- mu[i] + delta[i,k]                                   # model for linear predictor
     rhat[i,k] <- p[i,k] * n[i,k]                                            # expected value of the numerators
     dev[i,k] <- 2 * (r[i,k] * (log(r[i,k])-log(rhat[i,k]))             #Deviance contribution
         + (n[i,k]-r[i,k]) * (log(n[i,k]-r[i,k]) - log(n[i,k]-rhat[i,k])))
  }
  resdev[i] <- sum(dev[i,1:na[i]])            # summed residual deviance contribution for this trial
  for (k in 2:na[i]) {                                                   # LOOP THROUGH ARMS
     delta[i,k] ~ dnorm(md[i,k],taud[i,k])                      # trial-specific LOR distributions
     md[i,k] <- di[i,g[i,k]] - di[i,g[i,1]] + sw[i,k]     # mean of LOR distributions (with multi-arm correction)
     taud[i,k] <- tau *2*(k-1)/k                                    # precision of LOR distributions (with multi-arm correction)
     w[i,k] <- (delta[i,k] - di[i,g[i,k]] + di[i,g[i,1]])                 # adjustment for multi-arm RCTs
     sw[i,k] <- sum(w[i, 1:k-1])/(k-1)                            # cumulative adjustment for multi-arm trials
	}
  for (k in 1:ng){
	di[i,k]<-d[k]
	}
  di[i,12]<-pi2[i]*d[2]
  di[i,23]<-pi2[i]*d[2]+(1-pi2[i])*d[3]
}

totresdev <- sum(resdev[])                                           #Total Residual Deviance
d[1]<- 0                                                                      # group effect is zero for reference group
for (k in 2:ng)  { d[k] ~ dnorm(0,.0001)}                           # vague priors for group effects
sd ~ dunif(0,2)
tau <- pow(sd,-2)

# pairwise ORs and LORs for all possible pair-wise comparisons
for (c in 1:(ng-1)) {  for (k in (c+1):ng) {
       or[c,k] <- exp(d[k] - d[c])
       lor[c,k] <- (d[k]-d[c])
      }
}

  # Separate model to estimate proportion of patients in category two
  for (j in 1:ns_complete){
    n_complete2a [j] ~ dbin(theta1[j], n_complete12[j])
    n_complete2b [j] ~ dbin(theta2[j], n_complete23[j] )
    
    ## Make it a random effects model to allow for variation in the proportions
    logit(theta1[j]) <- theta1_tform[j]
    logit(theta2[j]) <- theta2_tform[j]
    
    theta1_tform[j] ~ dnorm(theta1_mu, theta1_prec)
    theta2_tform[j] ~ dnorm(theta2_mu, theta2_prec)
  }
  
  theta1_mu ~ dnorm(0, 0.001)
  theta1_prec <- pow(theta1_sd, -2)
  theta1_sd ~ dnorm(0, 0.01)T(0,)

  theta2_mu ~ dnorm(0, 0.001)
  theta2_prec <- pow(theta2_sd, -2)
  theta2_sd ~ dnorm(0, 0.01)T(0,)
  
  # Uncertainty parameters for pi2
for(i in 1:ns){                                                               # LOOP THROUGH STUDIES
  ## prediction for proportions in each study
  theta1_pred_lp[i] ~ dnorm(theta1_mu, theta1_prec)
  theta2_pred_lp[i] ~ dnorm(theta2_mu, theta2_prec)
  
  logit(theta1_pred[i]) <- theta1_pred_lp[i]
  logit(theta2_pred[i]) <- theta2_pred_lp[i]
  }
  
  ## transform theta1_mu into proportion for results
  logit(theta1_prop) <- theta1_mu
  logit(theta2_prop) <- theta2_mu
  
}                                                                                 # PROGRAM ENDS