model{                                                                       
for(i in 1:ns){                                                               # LOOP THROUGH STUDIES

  pi2[i] <- coll12[i] * theta1 + (1 - coll12[i])*theta2       # Impute pi2 for different collaps types
  delta[i,1]<-0
  mu[i] ~ dnorm(0,.0001)                                  # vague priors for all trial baselines
  for (k in 1:na[i]) {                                                       # LOOP THROUGH GROUPS
    r[i,k] ~ dbin(p[i,k],n[i,k])                                           # binomial likelihood
    logit(p[i,k]) <- mu[i] + delta[i,k]                                   # model for linear predictor
     rhat[i,k] <- p[i,k] * n[i,k]                                            # expected value of the numerators
     dev[i,k] <- 2 * (r[i,k] * (log(r[i,k])-log(rhat[i,k]))             #Deviance contribution
         + (n[i,k]-r[i,k]) * (log(n[i,k]-r[i,k]) - log(n[i,k]-rhat[i,k])))
  }
  resdev[i] <- sum(dev[i,1:na[i]])                        # summed residual deviance contribution for this trial
  for (k in 2:na[i]) {                                    # LOOP THROUGH ARMS
     delta[i,k] <-  di[i,g[i,k]] - di[i,g[i,1]]           # NMA model
  }
  for (k in 1:ng){
    di[i,k]<-d[k]
    }
  di[i,12]<-pi2[i]*d[2]
  di[i,23]<-pi2[i]*d[2]+(1-pi2[i])*d[3]
}

totresdev <- sum(resdev[])                                #Total Residual Deviance
d[1]<- 0                                                  # group effect is zero for reference group
for (k in 2:ng)  { d[k] ~ dnorm(0,.0001)}                 # vague priors for group effects

# pairwise ORs and LORs for all possible pair-wise comparisons
for (c in 1:(ng-1)) {  for (k in (c+1):ng) {
       or[c,k] <- exp(d[k] - d[c])
       lor[c,k] <- (d[k]-d[c])
      }
}
  # Uncertainty parameters for pi2
  theta1 ~ dbeta(1, 1)
  theta2 ~ dbeta(1, 1)
}                                                         # *** PROGRAM ENDS
